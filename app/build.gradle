apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

// Create a variable called keystorePropertiesFile, and initialize it to your
// keystore.properties file, in the rootProject folder.
def keystorePropertiesFile = rootProject.file("keystore.properties")

// Initialize a new Properties() object called keystoreProperties.
// Load your keystore.properties file into the keystoreProperties object.
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
	compileSdkVersion 31

	defaultConfig {
		applicationId "com.nfcalarmclock"
		minSdkVersion 21
		targetSdkVersion 31
		versionCode 308
		versionName "10.5.16"

		javaCompileOptions {
			annotationProcessorOptions {
				arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
			}
		}
	}

	signingConfigs {
		release {
			keyAlias keystoreProperties['keyAlias']
			keyPassword keystoreProperties['keyPassword']
			storeFile file(keystoreProperties['storeFile'])
			storePassword keystoreProperties['storePassword']
		}
	}

	buildTypes {
		release {
			signingConfig signingConfigs.release
			minifyEnabled true
			shrinkResources true
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
		}

		innerTest {
			matchingFallbacks = ['debug', 'release']
		}
	}

	compileOptions {
		// Flag to enable support for the new language APIs
		// Error: coreLibraryDesugaring configuration contains no dependencies.
		// If you intend to enable core library desugaring, please add dependencies
		// to coreLibraryDesugaring configuration
		// coreLibraryDesugaringEnabled true

		// Sets Java compatibility to Java 8
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}

    lint {
        disable 'UnnecessaryInterfaceModifier'
        enable 'ConvertToWebp', 'DalvikOverride', 'DuplicateStrings', 'IconExpectedSize', 'MinSdkTooLow', 'MissingRegistered', 'NegativeMargin', 'Registered', 'TypographyQuotes'
    }

    applicationVariants.all { variant ->
		variant.resValue "string", "versionName", variant.versionName
	}

}

repositories {
	mavenCentral()

	maven {
		url "https://jitpack.io"
	}

	//flatDir {
	//	dirs 'libs'
	//}
}

dependencies {
	def room_version = "2.4.0-alpha01"

	//implementation fileTree(dir: 'libs', include: ['*.jar'])
	implementation 'androidx.annotation:annotation:1.2.0'
	implementation 'androidx.appcompat:appcompat:1.3.0'
	implementation 'androidx.cardview:cardview:1.0.0'
	implementation 'androidx.coordinatorlayout:coordinatorlayout:1.1.0'
	implementation 'androidx.core:core:1.5.0'
	implementation 'androidx.fragment:fragment:1.3.5'
	implementation 'androidx.preference:preference:1.1.1'
	implementation 'androidx.recyclerview:recyclerview:1.2.1'
	implementation 'androidx.viewpager:viewpager:1.0.0'
	implementation 'com.google.android.material:material:1.3.0'

	// Room database
	implementation "androidx.room:room-runtime:$room_version"
	annotationProcessor "androidx.room:room-compiler:$room_version"

	// Media player
	implementation 'com.google.android.exoplayer:exoplayer:2.17.1'

	// Google Play in-app review
	implementation 'com.google.android.play:core:1.10.3'

	// Import the Firebase BoM
	implementation platform('com.google.firebase:firebase-bom:28.2.0')

	// Add the dependency for the Firebase SDK for Google Analytics
	// When using the BoM, don't specify versions in Firebase dependencies
	implementation 'com.google.firebase:firebase-analytics'
	implementation 'com.google.firebase:firebase-crashlytics'

	//implementation 'com.spotify.sdk:spotify-auth-release:1.1.0@aar'
	//implementation 'com.spotify.sdk:spotify-app-remote-release:0.6.1@aar'
	//implementation "com.google.code.gson:gson:2.8.5"
	//implementation 'com.github.kaaes:spotify-web-api-android:0.4.1@aar'
	//implementation 'com.squareup.retrofit:retrofit:1.9.0'
	//implementation 'com.squareup.retrofit2:retrofit:2.5.0'
	//implementation 'com.squareup.okhttp3:okhttp:3.14.1'
	//implementation 'com.squareup.okhttp:okhttp:2.2.0'
}
